module NSEO :

input  ACNSEO;
output ACAUTRE, R, O, V;

loop
   abort
      sustain R
   when ACNSEO;

   emit R; pause;	% par securite, rouge encore un instant

   repeat 4 times
      emit V; pause
   end repeat;
   emit O; pause;
   emit ACAUTRE
end loop

end module

%%%%%%%%%%

module feux :

input  ACNS, ACEO;
output RNS, REO, ONS, OEO, VNS, VEO;

relation ACNS # ACEO;

[
  await 5 tick;  % Les feux tricolores ne demarre qu'apres 5 tick.
  emit ACNS	 % Et on commence par le Nord-Sud
  ||
  run NSEO[signal ACNS/ACNSEO, ACEO/ACAUTRE, RNS/R, ONS/O, VNS/V ]
  ||
  run NSEO[signal ACEO/ACNSEO, ACNS/ACAUTRE, REO/R, OEO/O, VEO/V]
]

end module


%%%%%%%%%%

%{

$ esterel feux_tricolores1_1.strl -simul
$ gcc -m32 -o feux_tricolores1_1 feux_tricolores1_1.c -I $ESTEREL/include -L $ESTEREL/lib -lcsimul
$ feux_tricolores1_1
feux> ;;;;;;;;;;;;;;;;;;
--- Output: RNS REO 
--- Output: RNS REO 
--- Output: RNS REO 
--- Output: RNS REO 
--- Output: RNS REO 
--- Output: RNS REO 
--- Output: REO VNS 
--- Output: REO VNS 
--- Output: REO VNS 
--- Output: REO VNS 
--- Output: REO ONS 
--- Output: RNS REO 
--- Output: RNS VEO 
--- Output: RNS VEO 
--- Output: RNS VEO 
--- Output: RNS VEO 
--- Output: RNS OEO 
--- Output: RNS REO 
feux> ;;
--- Output: REO VNS 
--- Output: REO VNS 
feux>

}%
